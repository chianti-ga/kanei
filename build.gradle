plugins {
    id 'java'
    //Versioning
    id "org.ajoberstar.reckon" version "0.16.1"

    id 'idea'

    //Update deps
    id "com.github.ben-manes.versions" version "0.46.0"

    id "io.freefair.lombok" version "8.0.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'fr.skitou'

repositories {
    mavenCentral()
    maven {
        name "reposiliteRepositoryReleases"
        url "http://45.155.169.234:4578/releases"
        allowInsecureProtocol = true
        credentials(PasswordCredentials)
        authentication {
            basic(BasicAuthentication)
        }
    }
    maven {
        url 'https://jitpack.io'
    }
}


dependencies {
    implementation "fr.skitou.botcore:botcore:1.2.0-beta.0.0+20230617T230125Z"

    implementation 'com.github.walkyst:lavaplayer-fork:1.4.2'
    implementation("net.dv8tion:JDA:5.0.0-beta.10")
    implementation 'ch.qos.logback:logback-classic:1.4.7'

    implementation('org.hibernate.orm:hibernate-core:6.2.3.Final')
    implementation 'org.hibernate.orm:hibernate-c3p0:6.2.0.CR3'
    implementation 'org.hibernate.orm:hibernate-community-dialects:6.2.0.CR3'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.40.1.0'

    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}
java {
    withJavadocJar()
    withSourcesJar()
}

reckon {
    scopeFromProp()
    stageFromProp('release', 'beta', 'dev')
}


test {
    useJUnitPlatform()
}
tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

compileTestJava {
    //options.verbose(true)
    options.deprecation(true)
    options.compilerArgs.addAll(
            "-Xlint"
    )
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.skitou.kanei.KaneiMain'
        )
    }

    destinationDirectory.set file('build/libs/')
}
javadoc {
    options.encoding('UTF-8')
}

tasks.withType(org.gradle.jvm.tasks.Jar){
    def resourcesDir = sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()
    new File(resourcesDir, "kaneiversion.txt").text = this.version
}

task copyJar(type: Copy) {
    dependsOn 'jar'
    from jar.getArchiveFile()
    into file('build/libs/')
    rename { String fileName ->
        fileName.replace(this.version as CharSequence, "")
    }
}
shadowJar {
    dependsOn 'build'
    archiveFileName = "kanei-all.jar"
}