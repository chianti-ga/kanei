import java.util.jar.JarFile

plugins {
    id 'java'
    id 'idea'
    //Update deps
    id "com.github.ben-manes.versions" version "0.51.0"

    id "io.freefair.lombok" version "8.11"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'fr.skitou'

repositories {
    mavenCentral()
    maven {
        name "reposiliteRepositoryReleases"
        url "https://reposilite.skitou.fr/releases"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://maven.lavalink.dev/releases'
    }
    //mavenLocal()
    repositories {
        maven {
            url "https://maven.topi.wtf/releases"
        }
    }
}


dependencies {
    implementation "fr.skitou.botcore:botcore:1.2.1-162-g8dc4aa8"
    implementation 'io.sentry:sentry:7.18.0'

    //Disc
    implementation 'dev.arbjerg:lavaplayer:2.2.2'
    implementation "dev.lavalink.youtube:v2:1.11.1"
    implementation "com.github.topi314.lavasrc:lavasrc:4.3.0"

    implementation("net.dv8tion:JDA:5.2.1")

    implementation 'ch.qos.logback:logback-classic:1.5.12'

    implementation('org.hibernate.orm:hibernate-core:6.6.3.Final')
    implementation 'org.hibernate.orm:hibernate-c3p0:6.6.3.Final'
    implementation 'org.hibernate.orm:hibernate-community-dialects:6.6.5.Final'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.47.1.0'


    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'

    implementation 'com.google.code.gson:gson:2.11.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
}
idea {
    module {
        downloadJavadoc = true
    }
}
dependencyUpdates {
    revision = "dev"
}
java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}
tasks.withType(JavaCompile).configureEach {
    options.encoding('UTF-8')
}

compileTestJava {
    //options.verbose(true)
    options.deprecation(true)
    options.compilerArgs.addAll(
            "-Xlint"
    )
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.skitou.kanei.KaneiMain',
                'Implementation-Title': project.name,
                'BotCore-Version': getBotCoreVersion()
        )
    }

    destinationDirectory.set file('build/libs/')
}
javadoc {
    options.encoding('UTF-8')
}

tasks.register('copyJar', Copy) {
    dependsOn 'jar'
    from jar.getArchiveFile()
    into file('build/libs/')
    rename { String fileName ->
        fileName.replace(this.version as CharSequence, "")
    }
}

tasks.register('getVersionFromGit') {
    def command = 'git describe --tags'
    def version = command.execute().text.trim()
    println "Application version: ${version}"

    project.version = version
    jar.manifest.attributes['Implementation-Version'] = version

}
tasks.named('build') {
    dependsOn('getVersionFromGit')
}

shadowJar {
    dependsOn 'build'
    archiveFileName = "kanei-all.jar"
}

def getBotCoreVersion() {
    def artifactId = "botcore"
    def dependency = configurations.compileClasspath
            .getFiles()
            .find { it.name.contains(artifactId) }

    if (dependency) {
        def jarFile = new JarFile(dependency)
        def manifest = jarFile.getManifest()
        def botCoreVersion = manifest.mainAttributes.getValue("BotCore-Version")
        jarFile.close()

        return botCoreVersion
    } else {
        throw new GradleException("Dependency not found: $artifactId")
    }
}
